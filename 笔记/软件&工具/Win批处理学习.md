# Win批处理学习

起因：学习批处理就是为了把事情变的更简单，我们的宗旨就是能重复的事情尽量自动化，省下的时间干点别的不是更好么？？

批处理的本质，是一堆DOS命令按一定顺序排列而形成的集合，是的，其实就是在学 DOS 命令；所以说批处理和 Linux 下的 Shell 脚本还是差不多的；**批处理的最大好处就是不用编译，直接运行** ，用记事本就可以编辑

批处理的后缀一般是 `.bat` ；不过好像 `.cmd` 的后缀也是可以的，不多见

## 常用命令

其实就是些 DOS 命令，利用这些命令能玩出什么样的花样来呢，这个就要看想象力了....

### echo 命令

打开回显或关闭请求回显功能，或显示消息。如果没有任何参数，echo 命令将显示当前回显设置。

语法：`echo [{on　off}][message]`

Sample：`@echo off / echo hello world`

在实际应用中我们会把这条命令和重定向符号（也称为管道符号，一般用 `>` or `>>` ）结合来实现输入一些命令到特定格式的文件中.

### @ 命令

表示不显示 @ 后面的命令，cmd 在执行某个命令时，会**先把执行的命令打印出来**，再显示执行结果，在命令前加 @ 可以把命令隐藏，只显示执行结果

Sample：`@echo off`

上面的这个栗子可以说是最常见的，基本所有的批处理开头都会是这个开头，`echo off` 我们知道是关闭回显，也就是不显示执行的命令了，但是它本身也是命令，所以加个 @ 可以保证这条命令也不会显示了！

### rem

注释命令，类似于在 C 语言中的`/*--------*/`，它并不会被执行，只是起一个注释的作用，便于别人阅读和你自己日后修改。好像用 `::` 也是可以的

### pause

从名字也应该能看出来，是暂停命令。运行 Pause 命令时，将显示下面的消息：

Press any key to continue. . .(或：请按任意键继续. . .)

pause 命令会使程序挂起

### call 命令/start命令

都是调用其他 bat 或者可执行文件的命令

CALL 命令可以在批处理执行过程中调用另一个批处理，当另一个批处理执行完后，再继续执行原来的批处理

start 启动的外部程序在新窗口中运行，批处理程序继续往下执行，不理会外部程序的运行状况。

不同之处在于 call 脚本中可以使用 goto，且 call 命令允许变量在脚本之间传递， start 则不支持。

`start calc.exe` 即可打开 Windows 的计算器，对于 Start 命令的参数：

```
MIN 		开始时窗口最小化
SEPARATE 	在分开的空间内开始 16 位 Windows 程序
HIGH 		在 HIGH 优先级类别开始应用程序
REALTIME 	在 REALTIME 优先级类别开始应用程序
WAIT 		启动应用程序并等候它结束
parameters 	这些为传送到命令/程序的参数
```

执行的应用程序是 32-位 **GUI** 应用程序时，CMD.EXE 不等应用程序终止就返回命令提示。如果在命令脚本内执行，该新行为则不会发生。

### goto

跳转命令。程序指针跳转到指定的标签.

在批处理中允许以 `:XXX` 来构建一个标号，然后用 `GOTO XXX` 跳转到标号 `:XXX` 处，然后执行标号后的命令

``` bash
@echo off
:start
set /a var+=1
echo %var%
if %var% leq 3 GOTO start
pause

运行显示：
1
2
3
4
```

不认识不要紧，稍后会解释

### title

没什么可说的，可以设置窗口的标题

### color

设置默认的控制台前景和背景颜色。

格式：`COLOR [attr]`

attr：指定控制台输出的颜色属性，颜色属性由**两个十六进制数字**指定；第一个为背景，第二个则为前景。

每个数字可以为以下任何值之一:

> 0 = 黑色     8 = 灰色
>
> 1 = 蓝色       9 = 淡蓝色
>
> 2 = 绿色       A = 淡绿色
>
> 3 = 湖蓝色     B = 淡浅绿色
>
> 4 = 红色       C = 淡红色
>
> 5 = 紫色       D = 淡紫色
>
> 6 = 黄色       E = 淡黄色
>
> 7 = 白色       F = 亮白色

如果没有给定任何参数，该命令会将颜色还原到 CMD.EXE 启动时的颜色。

例如: `COLOR fc` 在亮白色上产生亮红色

## 逻辑处理

### IF

这个关键字对于程序员真是太熟悉了，它的使用规则有：

``` bash
IF [NOT] ERRORLEVEL number command
IF [NOT] string1==string2 command
IF [NOT] EXIST filename command
```

- 第一种形式：

  `IF ERRORLEVEL` 这个句子必须放在某一个命令的后面，执行命令后由 IF ERRORLEVEL 来判断命令的返回值。

  Number 的数字取值范围 0~255，判断时值的排列顺序应该由大到小。**返回的值大于等于指定的值时，条件成立**

- 第二种形式：

  string1 和 string2 都为字符的数据，**英文内字符的大小写将看作不同**，这个条件中的等于号必须是两个。

  条件相等后即执行后面的 command；

  检测当前变量的值做出判断，为了防止字符串中含有空格，可以在比较的两个数据上用 `{} / [] / ""` 包起来。

- 第三种形式：

  EXIST filename 为文件或目录存在的意思

和一般语言并没有太大差别，还是比较容易接受的

### setlocal与变量延迟

这一块是非常重要的一部分，要好好理解哦~

为了更好的说明问题，我们先引入一个例子。

``` bash
@echo off
set a=4
set a=5 & echo %a%
pause

结果：4
```

解说：为什么是 4 而不是 5 呢？在 echo 之前明明已经把变量 a 的值改成 5 了？

让我们先了解一下批处理运行命令的机制：

> 批处理读取命令时是**按行读取的**（另外例如 for 命令等，其后**用一对圆括号闭合的所有语句也当作一行**），在处理之前要完成必要的**预处理工作**，这其中就包括对该行命令中的变量赋值(或者说是变量的替换)。
>
> 我们现在分析一下，批处理在运行到这句 `set a=5 & echo %a%` 之前，先把这一句整句读取并做了预处理——对变量 a 赋了值(就是把变量 a 替换为了具体的值，也就是 4)，那么 `%a%` 当然就是 4 了！

而为了能够感知环境变量的动态变化，批处理设计了**变量延迟**。

简单来说，在读取了一条完整的语句之后，不立即对该行的变量赋值，而会**在某个单条语句执行之前再进行赋值(替换)**，也就是说“延迟”了对变量的赋值。

那么如何开启变量延迟呢？变量延迟又需要注意什么呢？举个例子说明一下：

``` bash
@echo off
setlocal enabledelayedexpansion
set a=4
set a=5 & echo !a!
pause

结果：5
```

解说：启动了变量延迟，得到了正确答案。变量延迟的启动语句是 `setlocal enabledelayedexpansion` ，并且变量要用一对叹号 `!!` 括起来（注意要用英文的叹号），否则就没有变量延迟的效果。

再来分析一下，首先 `setlocal enabledelayedexpansion` 开启变量延迟，然后 `set a=4` 先给变量 a 赋值为 4，`set a=5 & echo !a!` 这句是给变量 a 赋值为 5 并输出（由于启动了变量延迟，所以批处理能够感知到动态变化，即不是先给该行变量赋值，而是在运行过程中（从左到右）给变量赋值(替换)，因此此时 a 的值就是5了）。

再举一个例子巩固一下。

```bash
@echo off
setlocal enabledelayedexpansion
for /l %%i in (1,1,5) do (
  set a=%%i
  echo !a!
)
pause

结果：
1
2
3
4
5
```

解说：本例开启了变量延迟并用 `!!` 将变量扩起来，因此得到我们预期的结果。如果不用变量延迟会出现什么结果呢？结果是：

ECHO 处于关闭状态。

即没有感知到for语句中的动态变化。

提示：在没有开启变量延迟的情况下，某条命令行中的变量改变，**必须到下一条命令才能体现**。这一点也可以加以利用，看例子：**交换两个变量的值，且不用中间变量**

``` bash
@echo off
::目的：交换两个变量的值，但是不使用临时变量
::Code by JM 2007-1-24 [email=CMD@XP]CMD@XP[/email]
::出处：http://www.cn-dos.net/forum/viewthread.php?tid=27078

set var1=abc
set var2=123
echo 交换前： var1=%var1% var2=%var2%
set var1=%var2%& set var2=%var1%
echo 交换后： var1=%var1% var2=%var2%
pause
```

真是骚操作，不服不行.....

## 参考

http://xstarcd.github.io/wiki/windows/windows_cmd_summary.html